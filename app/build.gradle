plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs"
    id "kotlinx-serialization"
    id "kotlin-parcelize"
    id "dagger.hilt.android.plugin"
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.github.quillpad"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 8
        versionName "1.4.5-dev"

        testInstrumentationRunner "org.qosp.notes.TestRunner"
    }

    if (project.hasProperty("keystore")) {
        signingConfigs {
            release {
                def keystoreFileArg = project.property("keystore").toString()
                def storePassArg = project.property("storepass").toString()
                def keyName = project.property("keyalias").toString()
                def keypassArg = project.property("keypass").toString()
                storeFile file(keystoreFileArg)
                storePassword storePassArg
                keyAlias keyName
                keyPassword keypassArg
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            crunchPngs false
            if (project.hasProperty("keystore")) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "versions"
    productFlavors {
        googleFlavor {
            dimension "versions"
            buildConfigField "boolean", "IS_GOOGLE", "true"
        }
        defaultFlavor {
            dimension "versions"
            buildConfigField "boolean", "IS_GOOGLE", "false"
        }
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }

    kapt {
        javacOptions {
            option("-Adagger.fastInit=ENABLED")
            option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
        }
    }
    namespace 'org.qosp.notes'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1'
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.4"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.0"
    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.6"
    // AndroidX
    implementation "androidx.core:core-ktx:1.9.0"
    implementation "androidx.appcompat:appcompat:1.5.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    implementation "androidx.coordinatorlayout:coordinatorlayout:1.2.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.fragment:fragment-ktx:1.5.4"
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "androidx.media:media:1.6.0"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Navigation Component
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Material Components
    implementation "com.google.android.material:material:$material_version"

    // Room
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    testImplementation "androidx.room:room-testing:$room_version"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"

    // Security
    implementation "androidx.security:security-crypto:1.1.0-alpha04"

    // Flow Preferences
    implementation "com.github.tfcporciuncula.flow-preferences:flow-preferences:1.4.0"

    // DataStore Preferences
    implementation "androidx.datastore:datastore-preferences:$datastore_version"
    implementation "me.msoul:datastoreext:1.0.0"

    // Markwon
    implementation "io.noties.markwon:core:$markwon_version"
    implementation "io.noties.markwon:editor:$markwon_version"
    implementation "io.noties.markwon:linkify:$markwon_version"
    implementation "io.noties.markwon:ext-strikethrough:$markwon_version"
    implementation "io.noties.markwon:simple-ext:$markwon_version"
    implementation "io.noties.markwon:ext-tables:$markwon_version"
    implementation "io.noties.markwon:ext-tasklist:$markwon_version"
    implementation "me.saket:better-link-movement-method:2.2.0"

    // Work Manager
    implementation "androidx.work:work-runtime-ktx:$work_version"
    androidTestImplementation "androidx.work:work-testing:$work_version"

    // Coil
    implementation "io.coil-kt:coil:$coil_version"
    implementation "io.coil-kt:coil-video:$coil_version"
    implementation "io.coil-kt:coil-gif:$coil_version"

    // PhotoView
    implementation "com.github.chrisbanes:PhotoView:$photoview_version"

    // Hilt
    implementation "androidx.hilt:hilt-work:1.0.0"
    implementation "com.google.dagger:hilt-android:$hilt_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0"

    // ExoPlayer
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:exoplayer-ui:$exoplayer_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"

    // LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
}
